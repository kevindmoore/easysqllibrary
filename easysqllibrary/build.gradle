apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


ext {
    github = 'https://github.com/kevindmoore/easysqllibrary'
    githubUrl = 'https://github.com/kevindmoore/easysqllibrary.git'
    projName = 'easysqllibrary'
    projDescription = 'Library for SQLite'
    currentVersionCode = 12
    currentVersion = '2.0.0'
    groupId = 'com.mastertechsoftware.easysqllibrary'
    buildPath = 'com/mastertechsoftware/easysqllibrary/easysqllibrary/' + currentVersion
}

android {
    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode currentVersionCode
        versionName currentVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation libLogging
//    implementation project(':logging')
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    classpath += project.files(configurations.compile)
//    excludes = ['com.mastertechsoftware.logging.*']
    println(classpath.getFiles())
    failOnError false
}

task generateJavadocsJar(type: Jar) {
    excludes = ['**/*.kt']
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name projName
            description projDescription
            url github
            inceptionYear '2016'

            packaging 'aar'
            groupId groupId
            artifactId projName
            version currentVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection githubUrl
                url github

            }
            developers {
                developer {
                    name 'Kevin Moore'
                }
            }
        }
    }
}

//install.dependsOn artifacts

group = groupId
version = currentVersion

bintray {
    user = bintray_user
    key = bintray_key
    dryRun = false //Whether to run this as dry-run, without deploying
//    filesSpec {
//        from ('build/libs') {
//            include '*.jar'
//            rename ("sqlLibrary-" + currentVersion + "-(.*)\\.jar", projName + '-' + currentVersion + "-\$1.jar")
//        }
//        into buildPath
//        from ('build/outputs/aar') {
//            include 'sqlLibrary-release.aar'
//            rename {projName + '-' + currentVersion + ".aar"}
//        }
//        into buildPath
//        from ('build/poms') {
//            include '*.xml'
//            rename {projName + '-' + currentVersion + ".pom"}
//        }
//        into buildPath
//    }
    pkg {
        repo = 'maven'
        name = projName

        labels = ['android']
        licenses = ['Apache-2.0']
        vcsUrl = githubUrl
        websiteUrl = github
    }
    configurations = ['archives']
}

